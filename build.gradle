//file:noinspection GroovyAssignabilityCheck

plugins {
    id 'java'
    id 'maven-publish'
    id "org.sonarqube" version "2.7"
}

group 'com.timtrense'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    testCompile 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
    testCompile group: 'org.thymeleaf', name: 'thymeleaf', version: '3.0.12.RELEASE'
    testCompile group: 'org.openjdk.jmh', name: 'jmh-core', version: '1.28'
    testAnnotationProcessor group: 'org.openjdk.jmh', name: 'jmh-generator-annprocess', version: '1.28'

    compile group: 'org.projectlombok', name: 'lombok', version: '1.18.16'
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.16'
}

sourceSets {
    example {
        java {
            compileClasspath += sourceSets.main.compileClasspath
            compileClasspath += sourceSets.main.runtimeClasspath
            runtimeClasspath += sourceSets.main.runtimeClasspath
        }
    }
}

configurations {
    exampleCompile.extendsFrom mainCompile
    exampleRuntime.extendsFrom mainRuntime
}

test {
    useJUnitPlatform()
}

task benchmarkFatJar(type: Jar, dependsOn: testClasses) {
    archiveClassifier.set 'benchmark-fat'
    dependsOn compileJava
    manifest {
        attributes 'Main-Class': 'com.timtrense.template.SpeedTest'
    }
    from {
        sourceSets.test.output
        configurations.compile?.collect { it.isDirectory() ? it : zipTree(it) }
        configurations.testCompile?.collect { it.isDirectory() ? it : zipTree(it) }
    }
    with jar
}

task sourceJar(type: Jar) {
    archiveClassifier.set 'sources'
    from sourceSets.main.allJava
}

task javadocJar(type: Jar) {
    archiveClassifier.set 'javadoc'
    from javadoc
}

artifacts {
    archives benchmarkFatJar, sourceJar, javadocJar
}

publishing {
    publications {
        'nexus.timtrense.com'(MavenPublication) {
            artifact javadocJar
            artifact sourceJar
            artifact jar
            pom {
                name = rootProject.name
                description = rootProject.description
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = "timtrense"
                        name = "Tim Trense"
                        email = "trensetim@gmail.com"
                    }
                }
            }
        }
    }
    repositories {
        maven {
            url 'https://nexus.timtrense.com/repository/com.timtrense-SNAPSHOT/'
            credentials {
                username = "$mavenUsername"
                password = "$mavenPassword"
            }
        }
    }
}
